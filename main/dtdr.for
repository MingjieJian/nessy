      module MOD_DTDR
      contains
C**********  MODULNAME: DTDR      ******* 01/06/87  20.29.23.******    82 KARTEN
c*** this version was for the IMSL routine IPERK
c      SUBROUTINE DTDR(N,R,T,TPRIME)
c*** present version for the numerical recipes routines
      SUBROUTINE DTDR(R,T,TPRIME)
C*******************************************************************************
C***  THIS SUBROUTINE HELPS TO CALCULATE THE TEMPERATURE STRUCTURE OF A
C***  SPHERICAL, GREY ATMOSPHERE.
C***  IT IS A FORMAL PARAMETER OF THE IMSL-ROUTINE "IVPRK"(DVERK) AND CALLED 
C***  ONLY FROM THERE.
C***  THE DIFFERENTIAL EQUATION IS WRITTEN: Y' = F(X,Y)
C***  HERE:  DT/DR = FUNCTION OF (R,T)
C***  THE EDDINGTON FACTOR F IS ASSUMED TO BE 1/3 INSIDE THE RADIUS R1, I.E.
C***  WHERE TAU=1.
C***  OUTSIDE THE RADIUS R13, I.E. WHERE TAU=1/3, WE ASSUME GEOMETRICAL DILUTION
C***  DILUTION OF THE RADIATION FIELD AS FOR A UNIFORM RADIATING SPHERE
C***  OF RADIUS R23
C***  F IS INTERPOLATED IN THE INTERJACENT REGION
C*******************************************************************************

      IMPLICIT REAL*8(A-H,O-Z)
     
      PARAMETER ( ONE = 1.D+0, TWO = 2.D+0, THREE = 3.D+0 )
      
      COMMON /COMTEFF/ TEFF,TMIN,TMODIFY,SPHERIC
C***  OPAROSS FROM SUBROUTINE GREY
      COMMON /COMDTDR/ OPAMEAN,R23COM,R1COM,R13COM
     
      IF (R .LE. R1 COM) THEN
C***     OPTICALLY THICK REGION
          FEDDI=.33333333333333
          BFAC=0.
      ELSE IF (R .LT. R13COM) THEN
C***  INTERPOLATED REGION AROUND R23, I.E. BETWEEN TAU=1/3 AND TAU=1
C***  F1 = FEDDI AT R1
      F1=0.333333333333
C***  F2 = FEDDI AT R13
      R2=R13COM*R13COM
         SMUE=SQRT(ONE-R23COM*R23COM/R2)
      F2=((SMUE+ONE)*SMUE+ONE)/THREE
C***  F3 = FPRIME AT R1
      F3=0.
C***  F4 = FPRIME AT R13
      F4=(TWO+ONE/SMUE)*R23COM*R23COM/R2/R13COM/THREE
C***  POLYNOMIAL COEFFICIENTS, SEE SUBROUTINE CUBIC
      D1=ONE/(R13COM-R1COM)
      D2=D1*D1
      D3=D1*D2
      D23=D2/THREE
      H11=D3
      H12=-D3
      H13=D23
      H14=TWO*D23
      H21=-D1
      H22=TWO*D1
      H23=-0.333333333333333D0
      H24=-0.666666666666666D0
      H31=-D3
      H32=D3
      H33=-TWO*D23
      H34=-D23
      H41=TWO*D1
      H42=-D1
      H43=0.666666666666666D0
      H44=0.333333333333333D0
C***  CALCULATE POLYNOMIAL COEFFICIENTS: P(VECTOR) = H(MATRIX) * F(VECTOR)
      P1=H11*F1+H12*F2+H13*F3+H14*F4
      P2=H21*F1+H22*F2+H23*F3+H24*F4
      P3=H31*F1+H32*F2+H33*F3+H34*F4
      P4=H41*F1+H42*F2+H43*F3+H44*F4
C***  EVALUATION OF THE POLYNOMIAL
      FEDDI=P1*(R-R1COM)**3 + P2*(R-R1COM)
     +     +P3*(R13COM-R)**3+P4*(R13COM-R)
C***  ... AND ITS DERIVATIVE
      FPRIME=THREE*P1*(R-R1COM)**2 + P2
     -      -THREE*P3*(R13COM-R)**2-P4
          BFAC=(THREE*FEDDI-ONE)/R+FPRIME
      ELSE
          R2=R*R
         SMUE=SQRT(ONE-R23COM*R23COM/R2)
          FEDDI=((SMUE+ONE)*SMUE+ONE)/THREE
          FPRIME=(TWO+ONE/SMUE)*R23COM*R23COM/R2/R/THREE
          BFAC=(THREE*FEDDI-ONE)/R+FPRIME
      ENDIF
      TT=TEFF/T
      TT3=TT*TT*TT
      TPRIME=-BFAC*T/4./FEDDI-OPAMEAN/R/R*TEFF/16./FEDDI*TT3
      RETURN
      END subroutine
      end module
